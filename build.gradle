buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        def freefairGradleVersion = "6.3.0"
        classpath "io.freefair.gradle:git-plugin:${freefairGradleVersion}"
        classpath "io.freefair.gradle:lombok-plugin:${freefairGradleVersion}"
        classpath "io.freefair.gradle:maven-plugin:${freefairGradleVersion}"
    }
}

plugins {
    id "com.gradle.plugin-publish" version "0.20.0"
    id "java-gradle-plugin"
}

apply plugin: "io.freefair.git-version"
apply plugin: "maven-publish"
apply plugin: "io.freefair.lombok"
apply plugin: "io.freefair.javadoc-links"

repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
}

group = 'io.freefair.gradle'

java.withJavadocJar()

javadoc.options.links "https://docs.gradle.org/$gradle.gradleVersion/javadoc/"
javadoc.options.links "https://developer.android.com/reference/tools/gradle-api/7.0/"
javadoc.failOnError = false

compileJava.options.compilerArgs += "-Xlint"

sourceCompatibility = JavaVersion.VERSION_11

dependencies {
    api 'com.android.tools.build:gradle:7.0.4'
    api 'io.freefair.gradle:lombok-plugin:6.2.0'
    api 'io.freefair.gradle:aspectj-plugin:6.2.0'

    testImplementation 'junit:junit:4.13.2'
}

gradlePlugin {
    //noinspection GroovyAssignabilityCheck
    plugins {
        androidJavadoc {
            id = "io.freefair.android-javadoc"
            implementationClass = "io.freefair.gradle.plugins.android.AndroidJavadocPlugin"
        }
        androidLombok {
            id = "io.freefair.android-lombok"
            implementationClass = "io.freefair.gradle.plugins.android.AndroidLombokPlugin"
        }
        androidJavadocJar {
            id = "io.freefair.android-javadoc-jar"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidJavadocJarPlugin"
        }
        androidSourcesJar {
            id = "io.freefair.android-sources-jar"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidSourcesJarPlugin"
        }
        androidMavenJars {
            id = "io.freefair.android-maven-jars"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidMavenJarsPlugin"
        }
        androidSoftwareComponents {
            id = "io.freefair.android-software-components"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidComponentPlugin"
        }
        androidCi {
            id = "io.freefair.android-ci"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiPlugin"
        }
        androidCiDex {
            id = "io.freefair.android-ci-dex"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiDexPlugin"
        }
        androidCiLint {
            id = "io.freefair.android-ci-lint"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiLintPlugin"
        }
        androidCheckstyle {
            id = "io.freefair.android-checkstyle"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidCheckstylePlugin"
        }
        androidPmd {
            id = "io.freefair.android-pmd"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidPmdPlugin"
        }
        androidMavenPublish {
            id = "io.freefair.android-maven-publish"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidMavenPublishPlugin"
        }
        androidMavenPublishAllVariants {
            id = "io.freefair.android-maven-publish-all-variants"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidMavenPublishAllVariantsPlugin"
        }
        androidAspectjTransfrom {
            id = "io.freefair.android.aspectj.transform"
            implementationClass = "io.freefair.gradle.plugins.android.aspectj.AndroidAspectJTransformPlugin"
        }
        androidAspectjPostCompileWeaving {
            id = "io.freefair.android.aspectj.post-compile-weaving"
            implementationClass = "io.freefair.gradle.plugins.android.aspectj.AndroidAspectJPostCompileWeavingPlugin"
        }
    }
}

pluginBundle {
    website = "https://docs.freefair.io/android-gradle-plugins/$version/"
    vcsUrl = 'https://github.com/freefair/android-gradle-plugins'
    tags = ['android']

    plugins {
        androidJavadoc {
            displayName = "Android Javadoc Plugin"
            description = "Generate javadocs for all variants of your android project"
            tags = ['android', 'javadoc']
        }
        androidLombok {
            displayName = "Android Lombok Plugin"
            description = "Automatic Lombok and Delombok configuration for your android project"
            tags = ['android', 'lombok']
        }
        androidJavadocJar {
            displayName = "Android Javadoc Jar Plugin"
            description = "Generate javadoc jars for all variants of your android project"
            tags = ['android', 'maven', 'javadoc', 'jar']
        }
        androidSourcesJar {
            displayName = "Android Sources Jar Plugin"
            description = "Generate sources jars for all variants of your android project"
            tags = ['android', 'maven', 'sources', 'jar']
        }
        androidMavenJars {
            displayName = "Android Maven Jars Plugin"
            description = "Generate sources and javadoc jars for all variants of your android project"
            tags = ['android', 'maven', 'jar']
        }
        androidCi {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis']
        }
        androidCiDex {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis', 'dex']
        }
        androidCiLint {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis', 'lint']
        }
        androidCheckstyle {
            displayName = "Android Checkstyle Plugin"
            description = "Run Checkstyle on your android source-sets"
            tags = ['android', 'checkstyle']
        }
        androidPmd {
            displayName = "Android PMD Plugin"
            description = "Run PMD on your android source-sets"
            tags = ['android', 'pmd']
        }
        androidMavenPublish {
            displayName = "Android Maven Publish Plugin"
            description = "Android Maven Publish Plugin"
            tags = ['android', 'maven', 'publication', 'publishing']
        }
        androidMavenPublishAllVariants {
            displayName = "Android Maven Publish All Variants Plugin"
            description = "Android Maven Publish All Variants Plugin"
            tags = ['android', 'maven', 'publication', 'publishing']
        }
        androidAspectjTransfrom {
            displayName = "Android AspectJ Transform Plugin"
            description = "Android AspectJ Transform Plugin"
            tags = ['android', 'aspectj', 'ajc', 'weaving']
        }
        androidAspectjPostCompileWeaving {
            displayName = "Android AspectJ Post-Compile Weaving Plugin"
            description = "Android AspectJ Post-Compile Weaving Plugin"
        }
    }

    mavenCoordinates {
        groupId = project.group
    }
}

task uploadDocumentation(type: io.freefair.gradle.plugins.okhttp.tasks.UploadFile) {
    dependsOn "javadocJar"
    username = "user"
    password = findProperty('freefairDocsPass')
    file = javadocJar.archiveFile
    url = "https://docs.freefair.io/api/$project.version?path=android-gradle-plugins"
}
