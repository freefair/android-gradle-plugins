import io.freefair.gradle.plugins.okhttp.tasks.UploadFile

plugins {
    id "com.gradle.plugin-publish" version "1.1.0"
    id "io.freefair.git-version" version "6.6.1"
    id "io.freefair.lombok" version "6.6.1"
    id "io.freefair.javadoc-links" version "6.6.1"
}

repositories {
    gradlePluginPortal()
    google()
    mavenCentral()
}

group = 'io.freefair.gradle'

javadoc.options.links "https://docs.gradle.org/$gradle.gradleVersion/javadoc/"
javadoc.options.links "https://developer.android.com/reference/tools/gradle-api/7.4/"
javadoc.failOnError = false

jar {
    manifest {
        attributes 'Implementation-Version': "$project.version"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

test.useJUnitPlatform()

dependencies {
    api 'com.android.tools.build:gradle:7.4.0-rc01'
    api 'io.freefair.gradle:lombok-plugin:6.6.1'
    api 'io.freefair.gradle:aspectj-plugin:6.6.1'
    implementation 'io.freefair.gradle:plugin-utils:6.6.1'

    testImplementation platform("org.junit:junit-bom:5.9.1")

    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.mockito:mockito-core:4.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

gradlePlugin {
    //noinspection GroovyAssignabilityCheck
    plugins {
        androidJavadoc {
            id = "io.freefair.android-javadoc"
            implementationClass = "io.freefair.gradle.plugins.android.AndroidJavadocPlugin"
            displayName = "Android Javadoc Plugin"
            description = "Generate javadocs for all variants of your android project"
        }
        androidLombok {
            id = "io.freefair.android-lombok"
            implementationClass = "io.freefair.gradle.plugins.android.lombok.AndroidLombokPlugin"
            displayName = "Android Lombok Plugin"
            description = "Automatic Lombok and Delombok configuration for your android project"
        }
        androidCi {
            id = "io.freefair.android-ci"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiPlugin"
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"

        }
        androidCiLint {
            id = "io.freefair.android-ci-lint"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiLintPlugin"
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
        }
        androidAspectjPostCompileWeaving {
            id = "io.freefair.android.aspectj.post-compile-weaving"
            implementationClass = "io.freefair.gradle.plugins.android.aspectj.AndroidAspectJPostCompileWeavingPlugin"
            displayName = "Android AspectJ Post-Compile Weaving Plugin"
            description = "Android AspectJ Post-Compile Weaving Plugin"
        }
    }
}

pluginBundle {
    website = "https://docs.freefair.io/android-gradle-plugins/$version/"
    vcsUrl = 'https://github.com/freefair/android-gradle-plugins'
    tags = ['android']
}

tasks.register('uploadDocumentation', UploadFile) {
    dependsOn "javadocJar"
    username = "user"
    password = findProperty('freefairDocsPass')
    file = javadocJar.archiveFile
    url = "https://docs.freefair.io/api/$project.version?path=android-gradle-plugins"
}
