plugins {
    id "maven-publish"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.10.1"
    id "io.freefair.lombok" version "4.1.0"
    id 'io.freefair.git-version' version '4.1.0'
    id "io.freefair.aggregate-javadoc-jar" version "4.1.1"
    id "io.freefair.javadoc-links" version "4.1.0"
    id "jacoco"
}

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    google()
}

group = 'io.freefair.gradle'

def javaVersion = JavaVersion.current()

javadoc.options.links "https://docs.gradle.org/$gradle.gradleVersion/javadoc/"
javadoc.options.links "https://google.github.io/android-gradle-dsl/javadoc/3.4/"
javadoc.failOnError = false

compileJava.options.compilerArgs += "-Xlint"

sourceCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile 'com.android.tools.build:gradle:3.5.0'
    compile 'io.freefair.gradle:lombok-plugin:4.1.0'

    testCompile 'junit:junit:4.12'
}

gradlePlugin {
    //noinspection GroovyAssignabilityCheck
    plugins {
        androidJavadoc {
            id = "io.freefair.android-javadoc"
            implementationClass = "io.freefair.gradle.plugins.android.AndroidJavadocPlugin"
        }
        androidLombok {
            id = "io.freefair.android-lombok"
            implementationClass = "io.freefair.gradle.plugins.android.AndroidLombokPlugin"
        }
        androidJavadocJar {
            id = "io.freefair.android-javadoc-jar"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidJavadocJarPlugin"
        }
        androidSourcesJar {
            id = "io.freefair.android-sources-jar"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidSourcesJarPlugin"
        }
        androidMavenJars {
            id = "io.freefair.android-maven-jars"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidMavenJarsPlugin"
        }
        androidSoftwareComponents {
            id = "io.freefair.android-software-components"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidComponentPlugin"
        }
        androidCi {
            id = "io.freefair.android-ci"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiPlugin"
        }
        androidCiDex {
            id = "io.freefair.android-ci-dex"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiDexPlugin"
        }
        androidCiLint {
            id = "io.freefair.android-ci-lint"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiLintPlugin"
        }
        androidCheckstyle {
            id = "io.freefair.android-checkstyle"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidCheckstylePlugin"
        }
        androidPmd {
            id = "io.freefair.android-pmd"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidPmdPlugin"
        }
        androidFindbugs {
            id = "io.freefair.android-findbugs"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidFindBugsPlugin"
        }
        androidMavenPublish {
            id = "io.freefair.android-maven-publish"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidMavenPublishPlugin"
        }
        androidMavenPublishAllVariants {
            id = "io.freefair.android-maven-publish-all-variants"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidMavenPublishAllVariantsPlugin"
        }
    }
}

pluginBundle {
    website = "https://docs.freefair.io/android-gradle-plugins/$version/"
    vcsUrl = 'https://github.com/freefair/android-gradle-plugins'
    tags = ['android']

    plugins {
        androidJavadoc {
            displayName = "Android Javadoc Plugin"
            description = "Generate javadocs for all variants of your android project"
            tags = ['android', 'javadoc']
        }
        androidLombok {
            displayName = "Android Lombok Plugin"
            description = "Automatic Lombok and Delombok configuration for your android project"
            tags = ['android', 'lombok']
        }
        androidJavadocJar {
            displayName = "Android Javadoc Jar Plugin"
            description = "Generate javadoc jars for all variants of your android project"
            tags = ['android', 'maven', 'javadoc', 'jar']
        }
        androidSourcesJar {
            displayName = "Android Sources Jar Plugin"
            description = "Generate sources jars for all variants of your android project"
            tags = ['android', 'maven', 'sources', 'jar']
        }
        androidMavenJars {
            displayName = "Android Maven Jars Plugin"
            description = "Generate sources and javadoc jars for all variants of your android project"
            tags = ['android', 'maven', 'jar']
        }
        androidSoftwareComponents {
            displayName = "Android SoftwareComponents plugin"
            description = "Adds a software component for each android variant to project.components"
            tags = ['android', 'publishing']
        }
        androidCi {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis']
        }
        androidCiDex {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis', 'dex']
        }
        androidCiLint {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis', 'lint']
        }
        androidCheckstyle {
            displayName = "Android Checkstyle Plugin"
            description = "Run Checkstyle on your android source-sets"
            tags = ['android', 'checkstyle']
        }
        androidPmd {
            displayName = "Android PMD Plugin"
            description = "Run PMD on your android source-sets"
            tags = ['android', 'pmd']
        }
        androidFindbugs {
            displayName = "Android FindBugs Plugin"
            description = "Run FindBugs on your android source-sets"
            tags = ['android', 'findbugs']
        }
        androidMavenPublish {
            displayName = "Android Maven Publish Plugin"
            description = "Android Maven Publish Plugin"
            tags = ['android', 'maven', 'publication', 'publishing']
        }
        androidMavenPublishAllVariants {
            displayName = "Android Maven Publish All Variants Plugin"
            description = "Android Maven Publish All Variants Plugin"
            tags = ['android', 'maven', 'publication', 'publishing']
        }
    }

    mavenCoordinates {
        groupId = project.group
    }
}

task uploadDocumentation(type: io.freefair.gradle.plugins.okhttp.tasks.UploadFile) {
    dependsOn "aggregateJavadocJar"
    username = "user"
    password = findProperty('freefairDocsPass')
    file = aggregateJavadocJar.archiveFile
    url = "https://docs.freefair.io/api/$project.version?path=android-gradle-plugins"
}
