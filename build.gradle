plugins {
    id "maven-publish"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.10.0"
    id "findbugs"
    id "pmd"
    id "io.freefair.lombok" version "2.7"
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

group = 'io.freefair.gradle'

javadoc.options.links "https://docs.oracle.com/javase/8/docs/api/"
javadoc.options.links "https://docs.gradle.org/current/javadoc/"
javadoc.options.links "https://google.github.io/android-gradle-dsl/javadoc/3.1/"

compileJava.options.compilerArgs += "-Xlint"

sourceCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile 'com.android.tools.build:gradle:3.1.4'

    testCompile 'junit:junit:4.12'
}

gradlePlugin {
    //noinspection GroovyAssignabilityCheck
    plugins {
        androidJavadoc {
            id = "io.freefair.android-javadoc"
            implementationClass = "io.freefair.gradle.plugins.android.AndroidJavadocPlugin"
        }
        androidJavadocJar {
            id = "io.freefair.android-javadoc-jar"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidJavadocJarPlugin"
        }
        androidSourcesJar {
            id = "io.freefair.android-sources-jar"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidSourcesJarPlugin"
        }
        androidMavenJars {
            id = "io.freefair.android-maven-jars"
            implementationClass = "io.freefair.gradle.plugins.android.maven.AndroidMavenJarsPlugin"
        }
        androidCi {
            id = "io.freefair.android-ci"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiPlugin"
        }
        androidCiDex {
            id = "io.freefair.android-ci-dex"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiDexPlugin"
        }
        androidCiLint {
            id = "io.freefair.android-ci-lint"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiLintPlugin"
        }
        androidCheckstyle {
            id = "io.freefair.android-checkstyle"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidCheckstylePlugin"
        }
        androidPmd {
            id = "io.freefair.android-pmd"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidPmdPlugin"
        }
        androidFindbugs {
            id = "io.freefair.android-findbugs"
            implementationClass = "io.freefair.gradle.plugins.android.quality.AndroidFindBugsPlugin"
        }
    }
}

pluginBundle {
    website = "https://github.com/freefair/android-gradle-plugins"
    vcsUrl = 'https://github.com/freefair/android-gradle-plugins'
    tags = ['android']

    plugins {
        androidJavadoc {
            displayName = "Android Javadoc Plugin"
            description = "Generate javadocs for all variants of your android project"
            tags = ['android', 'javadoc']
        }
        androidJavadocJar {
            displayName = "Android Javadoc Jar Plugin"
            description = "Generate javadoc jars for all variants of your android project"
            tags = ['android', 'maven', 'javadoc', 'jar']
        }
        androidSourcesJar {
            displayName = "Android Sources Jar Plugin"
            description = "Generate sources jars for all variants of your android project"
            tags = ['android', 'maven', 'sources', 'jar']
        }
        androidMavenJars {
            displayName = "Android Maven Jars Plugin"
            description = "Generate sources and javadoc jars for all variants of your android project"
            tags = ['android', 'maven', 'jar']
        }
        androidCi {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis']
        }
        androidCiDex {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis', 'dex']
        }
        androidCiLint {
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
            tags = ['android', 'ci', 'travis', 'lint']
        }
        androidCheckstyle {
            displayName = "Android Checkstyle Plugin"
            description = "Run Checkstyle on your android source-sets"
            tags = ['android', 'checkstyle']
        }
        androidPmd {
            displayName = "Android PMD Plugin"
            description = "Run PMD on your android source-sets"
            tags = ['android', 'pmd']
        }
        androidFindbugs {
            displayName = "Android FindBugs Plugin"
            description = "Run FindBugs on your android source-sets"
            tags = ['android', 'findbugs']
        }
    }

    mavenCoordinates {
        groupId = project.group
    }
}
