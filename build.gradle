import io.freefair.gradle.plugins.okhttp.tasks.UploadFile

plugins {
    id "com.gradle.plugin-publish" version "1.2.0"
    id "io.freefair.git-version" version "6.6.3"
    id "io.freefair.lombok" version "8.1.0"
    id "io.freefair.javadoc-links" version "6.6.3"
}

repositories {
    gradlePluginPortal()
    google()
    mavenCentral()
}

def freefairGradleVersion = "6.6.3"
def agpVersion = "7.4.2"

group = 'io.freefair.gradle'
description = "FreeFair Android Gradle Plugins"

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption("Xdoclint:-missing", true)
    options.links "https://docs.gradle.org/$gradle.gradleVersion/javadoc/"
    options.links "https://developer.android.com/reference/tools/gradle-api/7.4/"
    options.links "https://docs.freefair.io/gradle-plugins/$freefairGradleVersion/api"
}

jar {
    manifest {
        attributes 'Implementation-Version': "$project.version"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

test.useJUnitPlatform()

dependencies {
    api "com.android.tools.build:gradle-api:$agpVersion"
    runtimeOnly "com.android.tools.build:gradle:$agpVersion"

    api "io.freefair.gradle:lombok-plugin:$freefairGradleVersion"
    api "io.freefair.gradle:aspectj-plugin:$freefairGradleVersion"

    testImplementation platform("org.junit:junit-bom:5.9.3")

    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

gradlePlugin {
    //noinspection GroovyAssignabilityCheck
    plugins {
        androidJavadoc {
            id = "io.freefair.android-javadoc"
            implementationClass = "io.freefair.gradle.plugins.android.AndroidJavadocPlugin"
            displayName = "Android Javadoc Plugin"
            description = "Generate javadocs for all variants of your android project"
        }
        androidLombok {
            id = "io.freefair.android-lombok"
            implementationClass = "io.freefair.gradle.plugins.android.lombok.AndroidLombokPlugin"
            displayName = "Android Lombok Plugin"
            description = "Automatic Lombok and Delombok configuration for your android project"
        }
        androidCi {
            id = "io.freefair.android-ci"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiPlugin"
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"

        }
        androidCiLint {
            id = "io.freefair.android-ci-lint"
            implementationClass = "io.freefair.gradle.plugins.android.ci.CiLintPlugin"
            displayName = "Android CI Plugin"
            description = "Properly configure your android project on CI servers (like travis)"
        }
        androidAspectjPostCompileWeaving {
            id = "io.freefair.android.aspectj.post-compile-weaving"
            implementationClass = "io.freefair.gradle.plugins.android.aspectj.AndroidAspectJPostCompileWeavingPlugin"
            displayName = "Android AspectJ Post-Compile Weaving Plugin"
            description = "Android AspectJ Post-Compile Weaving Plugin"
        }
    }
}

pluginBundle {
    website = "https://docs.freefair.io/android-gradle-plugins/$version/"
    vcsUrl = 'https://github.com/freefair/android-gradle-plugins'
    tags = ['android']
}

plugins.withId("maven-publish") {
    project.apply plugin: 'io.freefair.maven-central.validate-poms'

    publishing {
        publications.withType(MavenPublication).configureEach {
            pom {
                url = 'https://github.com/freefair/android-gradle-plugins'
                name = provider { project.description }
                description = provider { project.description }
                inceptionYear = '2016'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/freefair/android-gradle-plugins/blob/master/LICENSE'
                    }
                }
                organization {
                    name = 'FreeFair'
                    url = 'https://github.com/freefair'
                }
                developers {
                    developer {
                        id = 'larsgrefer'
                        name = 'Lars Grefer'
                        email = 'github@larsgrefer.de'
                        timezone = 'Europe/Berlin'
                    }
                }
                ciManagement {
                    system = 'GitHub Actions'
                    url = 'https://github.com/freefair/android-gradle-plugins/actions'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/freefair/android-gradle-plugins/issues'
                }
                scm {
                    connection = 'scm:git:https://github.com/freefair/android-gradle-plugins.git'
                    developerConnection = 'scm:git:git@github.com:freefair/android-gradle-plugins.git'
                    url = 'https://github.com/freefair/android-gradle-plugins/'
                }
            }
        }
    }
}

tasks.register('uploadDocumentation', UploadFile) {
    dependsOn "javadocJar"
    username = "user"
    password = findProperty('freefairDocsPass')
    file = javadocJar.archiveFile
    url = "https://docs.freefair.io/api/$project.version?path=android-gradle-plugins"
}
